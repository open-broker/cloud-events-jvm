plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
    id "maven-publish"
}

group = 'org.openbroker'
version = '1.0-SNAPSHOT'
description = 'Library for cloud events'

wrapper.gradleVersion = '5.4.1'

ext {
    kotlinLanguageVersion = '1.3'
    jdkVersion = JavaVersion.VERSION_1_8

    defaultEncoding = 'UTF-8'

    ver = [
        'kotlin' : '1.3.31',
        'junit'  : '5.4.2',
        'jackson': '2.9.9',
    ]

    servicePackage = 'org.openbroker.cloudevents'
}

sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

repositories {
    jcenter()
    mavenCentral()
}

dependencies {

    implementation(
        platform("com.fasterxml.jackson:jackson-bom:$ver.jackson"),
        platform("org.jetbrains.kotlin:kotlin-bom:$ver.kotlin"),
        "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
        "com.fasterxml.jackson.core:jackson-core",
        "com.fasterxml.jackson.module:jackson-module-kotlin",
        "com.fasterxml.jackson.datatype:jackson-datatype-jsr310",
    )

    testImplementation(
        platform("org.junit:junit-bom:$ver.junit"),
        "org.junit.jupiter:junit-jupiter-api"
    )
    testRuntime(
        "org.junit.jupiter:junit-jupiter-engine"
    )
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        javaParameters = true
        jvmTarget = jdkVersion
        freeCompilerArgs += ['-Xjsr305=strict', '-progressive']
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinLanguageVersion
    }
}

tasks.withType(JavaCompile).all {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-parameters', '-Xlint:unchecked']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

test {

    useJUnitPlatform()

    reports.junitXml.enabled false
    reports.html.enabled false

    failFast = System.getProperty('failFast') != null
    include servicePackage.replace('.', '/') + '/**/*Test.*'

    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'

        debug {
            events "started", "passed", "skipped", "failed"
        }

        info {
            events "passed", "skipped", "failed"
        }
    }
}
